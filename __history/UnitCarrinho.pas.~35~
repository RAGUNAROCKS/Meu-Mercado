unit UnitCarrinho;

interface

uses
  System.SysUtils, System.Types, System.UITypes, System.Classes, System.Variants,
  FMX.Types, FMX.Controls, FMX.Forms, FMX.Graphics, FMX.Dialogs, FMX.Objects,
  FMX.Controls.Presentation, FMX.StdCtrls, FMX.Layouts, FMX.Ani, FMX.ListBox,
  uFunctions, uLoading, System.JSON;

type
  TFrmCarrinho = class(TForm)
    LytToolbar: TLayout;
    LblTitulo: TLabel;
    ImgVoltar: TImage;
    LtyEndereco: TLayout;
    LblNome: TLabel;
    LblEndereco: TLabel;
    BtnFinalizar: TButton;
    Rectangle1: TRectangle;
    Layout1: TLayout;
    Label1: TLabel;
    LblSubtotal: TLabel;
    Layout2: TLayout;
    Label3: TLabel;
    LblTaxa: TLabel;
    Layout3: TLayout;
    Label5: TLabel;
    LblTotal: TLabel;
    Label7: TLabel;
    LblEndEntrega: TLabel;
    LtbProdutos: TListBox;
    procedure FormShow(Sender: TObject);
    procedure BtnFinalizarClick(Sender: TObject);
  private
    procedure AddProduto(id_produto: integer;
                         descricao, url_foto: string;
                         qtd, valor_unit: double);
    procedure CarregarCarrinho;
    procedure DownloadFoto(lb: TListBox);
    procedure ThreadPedidoTerminate(Sender: TObject);
    { Private declarations }
  public
    { Public declarations }
  end;

var
  FrmCarrinho: TFrmCarrinho;

implementation

{$R *.fmx}

uses UnitCatalogo, UnitPrincipal, Frame.ProdutoLista, DataModule.Mercado,
  DataModule.Usuario;

procedure TFrmCarrinho.AddProduto(id_produto: integer;
                                 descricao, url_foto: string;
                                 qtd, valor_unit: double);
var
    item: TListBoxItem;
    frame: TFrameProdutLista;
begin
    item := TListBoxItem.Create(LtbProdutos);
    item.Selectable := false;
    item.Text := '';
    item.Height := 80;
    item.Tag := id_produto;

    frame := TFrameProdutLista.Create(item);
    frame.ImgProduto.TagString := url_foto;
    frame.LblDescricao.Text := descricao;
    frame.LblQtd.Text := qtd.ToString + ' x ' + FormatFloat('R$ #,##0.00', valor_unit);
    frame.LblValor.Text := FormatFloat('R$ #,##0.00', qtd * valor_unit);

    item.AddObject(frame);

    LtbProdutos.AddObject(item);
end;

procedure TFrmCarrinho.DownloadFoto(lb: TListBox);
var
    t: TThread;
    frame: TFrameProdutLista;
begin
    // Carregar imagens...
    t := TThread.CreateAnonymousThread(procedure
    var
        i : integer;
    begin

        for i := 0 to lb.Items.Count - 1 do
        begin
            //sleep(1000);
            frame := TFrameProdutLista(lb.ItemByIndex(i).Components[0]);

            if frame.ImgProduto.TagString <> '' then
                LoadImageFromURL(frame.ImgProduto.Bitmap, frame.ImgProduto.TagString);
        end;

    end);

    t.Start;
end;

procedure TFrmCarrinho.ThreadPedidoTerminate(Sender: TObject);
begin
   TLoading.Hide;

    if Sender is TThread then
    begin
        if Assigned(TThread(Sender).FatalException) then
        begin
            showmessage(Exception(TThread(sender).FatalException).Message);
            exit;
        end;
    end;

    DmMercado.LimparCarrinhoLocal;
    close;
end;

procedure TFrmCarrinho.BtnFinalizarClick(Sender: TObject);
var
    t : TThread;
    jsonPedido: TJsonObject;
    arrayItem: TJSONArray;
begin
    TLoading.Show(FrmCarrinho, '');
    t := TThread.CreateAnonymousThread(procedure
    begin
       try
            jsonPedido := DmMercado.JsonPedido(lblSubTotal.TagFloat, lblTaxa.TagFloat, lblTotal.TagFloat);
            jsonPedido.AddPair('itens', DmMercado.JsonPedidoItem);

            tthread.Synchronize(TThread.CurrentThread, procedure
            begin
                showmessage(jsonPedido.ToJSON);
            end);

            DmMercado.InserirPedido(jsonPedido);
        finally
            jsonPedido.DisposeOf;
        end;
    end);
    t.OnTerminate := ThreadPedidoTerminate;
    t.Start;
end;

procedure TFrmCarrinho.CarregarCarrinho;
var
    subtotal: double;
begin
    try
        DmMercado.ListarCarrinhoLocal;
        DmMercado.ListarItemCarrinhoLocal;
        DmUsuario.ListarUsuarioLocal;

        // Dados Mercado...
        lblNome.Text := DmMercado.QryCarrinho.FieldByName('NOME_MERCADO').AsString;
        lblEndereco.Text := DmMercado.QryCarrinho.FieldByName('ENDERECO_MERCADO').AsString;
        lblTaxa.Text := FormatFloat('R$ #,##0.00', DmMercado.QryCarrinho.FieldByName('TAXA_ENTREGA').AsFloat);
        lblTaxa.TagFloat := DmMercado.QryCarrinho.FieldByName('TAXA_ENTREGA').AsFloat;


        // Dados Usuario...
        lblEndEntrega.Text := DmUsuario.QryUsuario.FieldByName('ENDERECO').AsString + ' - ' +
                              DmUsuario.QryUsuario.FieldByName('BAIRRO').AsString + ' - ' +
                              DmUsuario.QryUsuario.FieldByName('CIDADE').AsString + ' - ' +
                              DmUsuario.QryUsuario.FieldByName('UF').AsString;

        // Itens do carrinho...
        subtotal := 0;
        LtbProdutos.Items.Clear;
        with DmMercado.QryCarrinhoItem do
        begin
            while NOT EOF do
            begin
                AddProduto(FieldByName('id_produto').AsInteger,
                           FieldByName('nome_produto').AsString,
                           FieldByName('url_foto').AsString,
                           FieldByName('qtd').AsFloat,
                           FieldByName('valor_unitario').AsFloat);

                subtotal := subtotal + FieldByName('valor_total').AsFloat;

                Next;
            end;
        end;

        lblSubtotal.Text := FormatFloat('R$ #,##0.00', subtotal);
        lblTotal.Text := FormatFloat('R$ #,##0.00', subtotal + lblTaxa.TagFloat);

        // Carrega as fotos...
        DownloadFoto(LtbProdutos);

    except on ex:exception do
        showmessage('Erro ao carregar carrinho: ' + ex.Message);
    end;
end;

procedure TFrmCarrinho.FormShow(Sender: TObject);
begin
  CarregarCarrinho;
end;

end.
